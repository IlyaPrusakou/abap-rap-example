projection implementation in class zbp_pru_purcorderhdr_odata_pro unique;
strict ( 2 );
with managed instance filter;
use draft;
extensible;

define behavior for Zpru_PurcOrderHdr_ODATA_Proj alias OrderProj
use etag
extensible
{
  field ( modify ) orderDescription; // augment operation for virtual element (updatable virtual element)


  use create ( augment, precheck ); // augment and precheck must be implemented ABP for projection
  use update ( augment );
  use delete;

  use association _items_tp { create ( augment, precheck ); with draft; } // augment and precheck must be implemented on ABP projection
  use association _text_tp { create; with draft; }

  use action Edit;
  use action Activate;
  use action Discard;
  use action Prepare;
  use action Resume;

  use action checkOrder;
  use action sendOrderStatisticToAzure;
  use action ( precheck ) ChangeStatus deep parameter Zpru_D_New_Status_Redefine1; // redefined parameter; // precheck must be implemented on ABP projection
  use action createFromTemplate;
  use function getMajorSupplier;
  action sendToIDOC parameter ZPRU_D_IDOC_ADRESS;

  use function getAllItems result entity ZPRU_PURCORDERITEM_ODATA_PROJ; // projected result

  static function calculateOpenOrderValue parameter Zpru_D_SupplierID result [0..1] Zpru_D_OpenOrderTotalValue;

}

define behavior for Zpru_PurcOrderItem_ODATA_Proj alias ItemProj
extensible
{
  use update;
  use delete;

  use association _header_tp { with draft; }

  use action markAsUrgent;
  use function getInventoryStatus;

}

define behavior for Zpru_PurcOrderHdr_T_PROJ alias TextProj
{
  use update;
  use delete;
  use association _header_tp { with draft; }
}