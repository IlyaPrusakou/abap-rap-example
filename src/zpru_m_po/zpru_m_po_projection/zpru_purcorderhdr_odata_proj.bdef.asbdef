projection implementation in class zbp_pru_purcorderhdr_odata_pro unique;
strict ( 2 );
with managed instance filter; // combined with interface filter
use draft;

define behavior for Zpru_PurcOrderHdr_ODATA_Proj alias OrderProj
use etag
{
  field ( modify ) isShippingMethodHidden; // augment operation for virtual element

  use create ( augment, precheck ); // augment and precheck must be implemented ABP for projection
  use update;
  use delete;

  use association _items_tp { create ( augment, precheck ); with draft; } // augment and precheck must be implemented on ABP projection

  use action Edit;
  use action Activate;
  use action Discard;
  use action Prepare;
  use action Resume;

  use action checkOrder;
  use action sendOrderStatisticToAzure;
  use action ( precheck ) ChangeStatus deep parameter Zpru_D_New_Status_Redefine1; // redefined parameter; // precheck must be implemented on ABP projection
  use action createFromTemplate;
  use function getMajorSupplier;
  //  use function getStatusHistory deep result ZPRU_D_HistoryRoot_Redefine1; // redefined result // instead exposition I've defined my owm function version

  //  action sendToIDOC deep table parameter Zpru_PurcOrderHdr_Abstract; // completly new action on projection with deep nested parameter
  //  function getStatusHistory deep result [0..*] ZPRU_D_HistoryRoot;
  use function getAllItems result entity ZPRU_PURCORDERITEM_ODATA_PROJ; // projected result

}

define behavior for Zpru_PurcOrderItem_ODATA_Proj alias ItemProj
{
  use update;
  use delete;

  use association _header_tp { with draft; }

  use action markAsUrgent;
  use function getInventoryStatus;

  field ( modify ) isWarehouseLocationHidden; // augment operation for virtual element

}